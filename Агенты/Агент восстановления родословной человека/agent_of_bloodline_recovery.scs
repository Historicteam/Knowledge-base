agent_of_bloodline_recovery
	=> nrel_main_idtf:
	[агентная scp-программа восстановления родословной]  (* <- lang_ru;; *);
	[agent scp-program of bloodline recovery] (* <- lang_en;; *);
	<- agent_scp_program;;

scp_program -> agent_of_bloodline_recovery (*
-> rrel_params: .agent_of_bloodline_recovery_params (*
-> rrel_1: rrel_in: _event;;
-> rrel_2: rrel_in: _input_arc;;
*);;

-> rrel_operators: .agent_of_bloodline_recovery_operator_set 
(*

-> rrel_init: .agent_of_bloodline_recovery_operator1 
(*
	<- searchElStr3;;
	-> rrel_1: rrel_assign: rrel_scp_var: _temp;;
	-> rrel_2: rrel_fixed: rrel_scp_var: _input_arc;;
	-> rrel_3: rrel_assign: rrel_scp_var: _quest;;

	=> nrel_goto: .agent_of_bloodline_recovery_operator2;;
*);;

-> .agent_of_bloodline_recovery_operator2
(*
	<- searchElStr3;;
	-> rrel_1: rrel_fixed: rrel_scp_const: 	question_of_bloodline_recovery;;
	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
	-> rrel_3: rrel_fixed: rrel_scp_var: _quest;;

	=> nrel_then: .agent_of_bloodline_recovery_operator3;;
	=> nrel_else: .agent_of_bloodline_recovery_operator_return;;
*);;

-> .agent_of_bloodline_recovery_operator3 
(*
	<- searchElStr3;;
	-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
	-> rrel_3: rrel_assign: rrel_scp_var: _outline;;

	=> nrel_then: .agent_of_bloodline_recovery_operator4;;
	=> nrel_else: .agent_of_bloodline_recovery_operator_return;;
*);;

-> .agent_of_bloodline_recovery_operator4
(*
	<- genEl;;
	-> rrel_1: rrel_assign: rrel_node: rrel_scp_var: _answer;;

	=> nrel_goto: .agent_of_bloodline_recovery_operator5;; 
*);;

-> .agent_of_bloodline_recovery_operator5 
(*
	<- call;;
	-> rrel_1: rrel_fixed: rrel_scp_const: proc_of_bloodline_recovery;;
	-> rrel_2: rrel_fixed: rrel_scp_const: .agent_of_bloodline_recovery_operator5_params 
	(*
		-> rrel_1: rrel_fixed: rrel_scp_var: _outline;;
		-> rrel_2: rrel_fixed: rrel_scp_var: _answer;;
	*);;
	-> rrel_3: rrel_assign: rrel_scp_var: _descr;;

	=> nrel_goto: .agent_of_bloodline_recovery_operator6;;
*);;

-> .agent_of_bloodline_recovery_operator6 
(*
	<- waitReturn;;
	-> rrel_1: rrel_fixed: rrel_scp_var: _descr;;

	=> nrel_goto: .agent_of_bloodline_recovery_operator7;;
*);;

-> .agent_of_bloodline_recovery_operator7
(*
	<- genElStr5;;
	-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
	-> rrel_2: rrel_assign: rrel_const: rrel_common: rrel_scp_var: _arc;;
	-> rrel_3: rrel_fixed: rrel_scp_var: _answer;;
	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
	-> rrel_5: rrel_fixed: rrel_scp_const: nrel_answer;;

	=> nrel_goto: .agent_of_bloodline_recovery_operator_return;;
*);; 

-> .agent_of_bloodline_recovery_operator_return 
(*
	<- return;;
*);;

*);;

*);;

